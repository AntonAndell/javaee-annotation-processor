buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'foundation.icon:gradle-javaee-plugin:0.7.8'
    }
}

subprojects {
    repositories {
//        maven {
//            url 'http://ci.arch.iconloop.com/nexus/repository/maven-public/'
//        }
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    ext {
        javapoetVersion = getProperty("javapoet.version")
        javaeeVersion = getProperty("javaee.version")
        scorexVersion = getProperty("scorex.version")
        javaeeUnittestVersion = getProperty("javaee-unittest.version")
        iconsdkVersion = getProperty("iconsdk.version")
        jacksonVersion = getProperty("jackson.version")
    }
}

configure([
        project(':annotation_processor'),
        project(':score-json'),
        project(':score-data'),
        project(':score-client'),
]) {
    apply plugin: 'maven-publish'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    group = getProperty("group")
    version = getProperty(project.name+".version") + (rootProject.hasProperty('release') ? '' : '-SNAPSHOT')

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                afterEvaluate {
                    artifactId = 'javaee-'+ project.name.replaceAll("_","-")
                }
            }
        }
        repositories {
            maven {
                name = 'ciNexus'
                def releasesUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-releases"
                def snapshotsUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-snapshots"
                url = rootProject.hasProperty('release') ? releasesUrl : snapshotsUrl
                credentials {
                    username = "$nexusUsername"
                    password = "$nexusPassword"
                }
            }
        }
    }
}
