buildscript {
    repositories {
        maven {
            url 'http://ci.arch.iconloop.com/nexus/repository/maven-public/'
        }
        mavenCentral()
        maven {
            url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
    }
    dependencies {
        classpath 'foundation.icon:gradle-javaee-plugin:0.7.5'
    }
}

apply plugin: 'foundation.icon.javaee'

version = '0.1.0'

// need to add this option to retrieve formal parameter names
compileJava {
    options.compilerArgs += ['-parameters']
}

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.8.7'
    implementation 'com.github.sink772:javaee-scorex:0.5.1'
    implementation 'com.github.sink772:minimal-json:0.9.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'

    compileOnly project(':annotation_processor')
    annotationProcessor project(':annotation_processor')
}

optimizedJar {
    mainClassName = 'com.iconloop.score.example.HelloWorld'
    archivesBaseName = 'hello-world'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

deployJar {
    endpoints {
        gangnam {
            uri = 'https://gicon.net.solidwallet.io/api/v3'
            nid = 7
        }
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 3
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
    parameters {
        arg('_name', 'Alice')
    }
}

test {
    useJUnitPlatform()
}
